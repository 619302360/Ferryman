// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'base'
buildscript {
    ext {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        bintrayUser = properties.getProperty('bintrayUser')
        bintrayKey = properties.getProperty('bintrayKey')

        ferryman_group = 'com.jude'
        ferryman_version = '1.4.1-alpha12'
        ferryman_version_compile = '1.3.1-alpha1'
    }
    repositories {
        google()
        jcenter()
        maven {
            url "https://dl.bintray.com/jude95/maven"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.novoda:bintray-release:0.8.1'
        classpath "com.jude:ferryman-modular:$ferryman_version_compile"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task uploadAll { task ->
    task.group = 'upload'
    task.dependsOn build
}

File localPropertiesFile = rootProject.file('local.properties')
Properties localProperties = new Properties()
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
    if (localProperties['bintray.user'] == null || localProperties['bintray.apiKey'] == null) {
        logger.log(LogLevel.ERROR, "No bintray properties!")
    }
}

subprojects { pro ->
    if (!pro.name.startsWith("ferryman-")) return
    version = ferryman_version
    group = ferryman_group

    apply plugin: 'com.novoda.bintray-release'
    publish {
        userOrg = "jude95"
        groupId = 'com.jude'
        artifactId = pro.name
        publishVersion = version
        desc = 'A tool for Android'
        website = 'https://github.com/Jude95/Ferryman'
        dryRun = false
    }
    bintrayUser = localProperties['bintray.user']
    bintrayKey = localProperties['bintray.apiKey']

    pro.afterEvaluate {
        uploadAll.dependsOn pro.tasks.getByName("bintrayUpload")
    }
}

